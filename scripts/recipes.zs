import crafttweaker.api.recipe.FurnaceRecipeManager;
import crafttweaker.api.misc.Composter;

var air = <item:minecraft:air>;
var book = <item:minecraft:book>;
var glowstone_block = <item:minecraft:glowstone>;
var glowstone_dust = <item:minecraft:glowstone_dust>;
var bookshelves = <tag:items:bliss:bookshelves>;
var brimstone = <item:biomesoplenty:brimstone>;
var canvas = <item:farmersdelight:canvas>;
var crop_tag = <tag:items:quark:seed_pouch_holdable>;
var heart_of_the_sea = <item:minecraft:heart_of_the_sea>;
var honey_bottle = <item:minecraft:honey_bottle>;
var iron = <tag:items:forge:ingots/iron>;
var ladder = <item:minecraft:ladder>;
var leather = <item:minecraft:leather>;
var oak_logs = <tag:items:minecraft:oak_logs>;
var oak_planks = <item:minecraft:oak_planks>;
var paraglider = <item:paraglider:paraglider>;
var piston = <item:minecraft:piston>;
var planks = <tag:items:minecraft:planks>;
var prismarine_shard = <item:minecraft:prismarine_shard>;
var quartz = <item:minecraft:quartz>;
var rotten_flesh = <item:minecraft:rotten_flesh>;
var sawmill = <item:corail_woodcutter:oak_woodcutter>;
var slime = <item:minecraft:slime_ball>;
var stick = <item:minecraft:stick>;
var string_item = <item:minecraft:string>;
var rose_quartz_block = <item:biomesoplenty:rose_quartz_block>;
var rose_quartz_shard = <item:biomesoplenty:rose_quartz_shard>;
var paper = <item:minecraft:paper>;
var antique_ink = <item:supplementaries:antique_ink>;

var axes = <tag:items:forge:tools/axes>;
var pickaxes = <tag:items:forge:tools/pickaxes>;
var knives = <tag:items:forge:tools/knives>;

// Remove some recipes that shouldn't be in here
craftingTable.remove(paraglider);
craftingTable.removeByName("minecraft:painting");
craftingTable.removeByName("minecraft:lead");
craftingTable.removeByName("minecraft:scaffolding");
craftingTable.removeByName("comforts:rope_and_nail");

// Craftable Trident
craftingTable.addShaped("ez_trident",
		<item:minecraft:trident>,
		[[air, quartz, heart_of_the_sea],
		 [air, prismarine_shard, quartz],
		 [prismarine_shard, air, air]]);

// Replace Rabbit Hide in recipes with Leather
craftingTable.removeByName("quark:tools/crafting/seed_pouch");
craftingTable.removeByName("quark:tools/crafting/bundle");
craftingTable.removeByName("supplementaries:slingshot");

craftingTable.addShaped("leather_seed_pouch",
		<item:quark:seed_pouch>,
		[[string_item, leather, string_item],
		 [leather, crop_tag, leather],
		 [air, leather, air]]);
craftingTable.addShaped("leather_bundle",
		<item:minecraft:bundle>,
		[[string_item, leather, string_item],
		 [leather, air, leather],
		 [leather, leather, leather]]);
 craftingTable.addShaped("leather_slingshot",
		<item:supplementaries:slingshot>,
		[[string_item, leather, string_item],
		 [stick, slime, stick],
		 [air, stick, air]]);

// Pre-End Iron Rod
craftingTable.removeByName("quark:automation/crafting/iron_rod");
craftingTable.addShaped("earlygame_iron_rod",
		<item:quark:iron_rod>,
		[[iron], [iron], [iron]]);

// RF to Leather
furnace.addRecipe("rf2leather", leather, rotten_flesh, 0.0, 200);

// Recycle Paraglider
CuttingBoard.addRecipe("recycle_paraglider", leather * 4, paraglider, knives);

// Honey Sticky Piston
craftingTable.addShaped("honey_sticky_piston",
		<item:minecraft:sticky_piston>,
		[[honey_bottle],
		[piston]]);

// Brimstone to Blaze
craftingTable.addShapeless("brim_blaze", <item:minecraft:blaze_powder>, [brimstone]);

// Planks to Ash
furnace.addRecipe("pl2ash", <item:supplementaries:ash>, planks, 0.0, 40);

// Woodcutting oak to ladder
Woodcutter.removeRecipe(ladder);
Woodcutter.addRecipe("oak_ladder", ladder, oak_planks);
Woodcutter.addRecipe("oak_ladder4", ladder * 4, oak_logs);

// Any Planks for sawmill
craftingTable.remove(sawmill);
craftingTable.addShaped("sawmill", sawmill,
	[[air, iron, air],
	[planks, planks, planks]]);

// lol, lmao
craftingTable.addShapeless("cry_about_it", <item:minecraft:crying_obsidian>, [<item:minecraft:obsidian>, <item:farmersdelight:onion>]);

// Non lethal recipes for supp stuff, autogenerated
craftingTable.addShapeless("honey_soap",
	<item:supplementaries:soap> * 4,
	[<item:minecraft:water_bucket>, <tag:items:forge:ash>, <tag:items:forge:ash>, <tag:items:forge:ash>, <tag:items:forge:ash>, <item:minecraft:honeycomb>]);
craftingTable.addShaped("honey_sling", <item:supplementaries:slingshot>,
	[[<item:minecraft:string>, <item:minecraft:leather>, <item:minecraft:string>],
	[<item:minecraft:stick>, <item:minecraft:honey_bottle>, <item:minecraft:stick>],
	[<item:minecraft:air>, <item:minecraft:stick>, <item:minecraft:air>]]);

// Bookshelf cut to book
CuttingBoard.addRecipe("remove_book", book * 3, bookshelves, axes);

// Glass cuts into shards
var glassBreak = "minecraft:block.glass.break";

CuttingBoard.addRecipeWithSound("clear_glass_sharding", <item:quark:clear_shard> * 4, <item:minecraft:glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("dirty_glass_sharding", <item:quark:dirty_shard> * 4, <item:quark:dirty_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("black_glass_sharding", <item:quark:black_shard> * 4, <item:minecraft:black_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("blue_glass_sharding", <item:quark:blue_shard> * 4, <item:minecraft:blue_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("brown_glass_sharding", <item:quark:brown_shard> * 4, <item:minecraft:brown_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("cyan_glass_sharding", <item:quark:cyan_shard> * 4, <item:minecraft:cyan_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("gray_glass_sharding", <item:quark:gray_shard> * 4, <item:minecraft:gray_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("green_glass_sharding", <item:quark:green_shard> * 4, <item:minecraft:green_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("light_blue_glass_sharding", <item:quark:light_blue_shard> * 4, <item:minecraft:light_blue_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("light_gray_glass_sharding", <item:quark:light_gray_shard> * 4, <item:minecraft:light_gray_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("lime_glass_sharding", <item:quark:lime_shard> * 4, <item:minecraft:lime_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("magenta_glass_sharding", <item:quark:magenta_shard> * 4, <item:minecraft:magenta_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("orange_glass_sharding", <item:quark:orange_shard> * 4, <item:minecraft:orange_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("pink_glass_sharding", <item:quark:pink_shard> * 4, <item:minecraft:pink_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("purple_glass_sharding", <item:quark:purple_shard> * 4, <item:minecraft:purple_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("red_glass_sharding", <item:quark:red_shard> * 4, <item:minecraft:red_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("white_glass_sharding", <item:quark:white_shard> * 4, <item:minecraft:white_stained_glass>, pickaxes, glassBreak);
CuttingBoard.addRecipeWithSound("yellow_glass_sharding", <item:quark:yellow_shard> * 4, <item:minecraft:yellow_stained_glass>, pickaxes, glassBreak);

// Glowstone cuts into dust
CuttingBoard.addRecipeWithSound("glowstone_dusting", glowstone_dust * 4, glowstone_block, pickaxes, glassBreak);

// Rose Quartz decompress
CuttingBoard.addRecipe("rose_sharding", rose_quartz_shard * 4, rose_quartz_block, pickaxes);

// Compostable Overweight Crops
composter.setValue(<item:overweight_farming:overweight_beetroot_block>, 1.0);
composter.setValue(<item:overweight_farming:overweight_carrot_block>, 1.0);
composter.setValue(<item:overweight_farming:overweight_cocoa_block>, 1.0);
composter.setValue(<item:overweight_farming:overweight_potato_block>, 1.0);
composter.setValue(<item:overweight_farming:overweight_onion_block>, 1.0);
composter.setValue(<item:overweight_farming:overweight_cabbage_block>, 1.0);

// Craftable Name Tag
craftingTable.addShapeless("da_name_tagy", <item:minecraft:name_tag>, [antique_ink, paper, string_item]);
